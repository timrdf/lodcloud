prefix void:     <http://rdfs.org/ns/void#>
prefix datafaqs: <http://purl.org/twc/vocab/datafaqs#>
prefix tag:      <http://www.holygoat.co.uk/owl/redwood/0.1/tags/>

construct {
   ?dataset                    # <http://thedatahub.org/dataset/DBpedia>
      a datafaqs:CKANDataset;
      void:subset ?linkset;
   .
   ?dataset tag:taggedWithTag ?tag .
   ?dataset void:triples      ?triples .

   ?linkset                    # <http://instances.tw.rpi.edu/id/linkset/DBpedia/e977476546bf11f68176d67246280e63>
      void:target  ?target;    # <http://thedatahub.org/dataset/aemet>
      void:triples ?overlap;   # 82
   .
   ?target a datafaqs:CKANDataset .
}
where {
   ?dataset                    # <http://thedatahub.org/dataset/DBpedia>
      a datafaqs:CKANDataset;
      void:subset ?linkset;
   .
   optional{ ?dataset tag:taggedWithTag ?tag }
   optional{ ?dataset void:triples      ?triples }

   ?linkset                    # <http://instances.tw.rpi.edu/id/linkset/DBpedia/e977476546bf11f68176d67246280e63>
      void:target  ?target;    # <http://thedatahub.org/dataset/aemet>
      void:triples ?overlap;   # 82
   .
   ?target a datafaqs:CKANDataset .
   filter(?dataset != ?target)
}
