prefix dcterms:    <http://purl.org/dc/terms/>
prefix void:       <http://rdfs.org/ns/void#>
prefix conversion: <http://purl.org/twc/vocab/conversion/>
prefix datafaqs:   <http://purl.org/twc/vocab/datafaqs#>
prefix tag:        <http://www.holygoat.co.uk/owl/redwood/0.1/tags/>

construct {
   ?dataset                    # <http://thedatahub.org/dataset/DBpedia>
      a datafaqs:CKANDataset;
      void:subset ?linkset
   .
   ?dataset tag:taggedWithTag ?tag .
   ?dataset void:triples      ?triples .

   ?linkset                    # <http://instances.tw.rpi.edu/id/linkset/DBpedia/e977476546bf11f68176d67246280e63>
      a void:Linkset;
      void:target  ?target;    # <http://thedatahub.org/dataset/aemet>
      void:target  ?dataset;   # <http://thedatahub.org/dataset/DBpedia>
      void:triples ?overlap    # 82
   .
   ?target a datafaqs:CKANDataset .
}
where {
   {
      select ?version
      where {
         <http://purl.org/twc/lodcloud/source/datahub-io/dataset/lodcloud-group>
            void:subset ?version .
                        ?version a conversion:VersionedDataset;
                           dcterms:modified ?date .
      }
      order by desc(?date)
      limit 1
   }
   graph ?version {
      ?dataset                    # <http://thedatahub.org/dataset/DBpedia>
         a datafaqs:CKANDataset
      .
      optional{ ?dataset tag:taggedWithTag ?tag }
      optional{ ?dataset void:triples      ?triples }

      optional {
         ?dataset void:subset ?linkset .
         ?linkset                 # <http://instances.tw.rpi.edu/id/linkset/DBpedia/e977476546bf11f68176d67246280e63>
            void:target  ?target; # <http://thedatahub.org/dataset/aemet>
            void:triples ?overlap # 82
         .
         ?target a datafaqs:CKANDataset .
         filter(?dataset != ?target)
      }
   }
}
