prefix void:       <http://rdfs.org/ns/void#>
prefix conversion: <http://purl.org/twc/vocab/conversion/>
prefix datafaqs:   <http://purl.org/twc/vocab/datafaqs#>
prefix prov:       <http://www.w3.org/ns/prov#>
prefix dcterms:    <http://purl.org/dc/terms/>
prefix rt:         <http://purl.org/rl/rt/>

#select ?dataset ?revision ?created ?agent ?description
construct {
   ?dataset a rt:RecurringThing .
   ?revision
      prov:specializationOf ?dataset;
      dcterms:created       ?created;
      dcterms:description   ?description;
      prov:wasAttributedTo  ?agent .
}
where {
   {
      select ?version
      where {
         <http://purl.org/twc/lodcloud/source/datahub-io/dataset/lodcloud-group>
            void:subset ?version .
                        ?version a conversion:VersionedDataset;
                           dcterms:modified ?date .
      }
      order by desc(?date)
      limit 1
   }
   graph ?version {
      ?dataset                    # <http://datahub.io/dataset/2000-us-census-rdf>
         a datafaqs:CKANDataset;
         prov:wasDerivedFrom ?revision
      .
      ?revision
       dcterms:created      ?created;
       dcterms:description  ?description;
       prov:wasAttributedTo ?agent .
   }
}
